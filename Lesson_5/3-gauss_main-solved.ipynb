{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Метод Гаусса с выбором главного элемента\n",
    "В методе Гаусса используется деление на диагональный элемент. Возникает вопрос: что будет, если диагональный элемент равен нулю или близок к нему? В этом случае метод Гаусса не даёт устойчивого решения. Более того, метод Гаусса устойчив только для матриц с диагональным преобладанием. Если диагонального преобладания нет, то можно использовать модификации метода Гаусса, которые считаются условно устойчивыми:\n",
    "\n",
    "1. С выбором главного элемента в строке\n",
    "2. С выбором главного элемента в столбце\n",
    "3. С выбором главного элемента по всей матрице\n",
    "\n",
    "Рассмотрим первый метод - с выбором главного элемента в строке. Вспомним, как начинался оригинальный метод: нужно было первую строку, умноженную на коэффициент вычесть из всех остальных. В модифицированном методе перед тем, как это делать, нужно найти в первой строке максимальный по модулю элемент и поменять местами первый столбец и столбец, содержащий максимальный элемент. Потом то же самое нужно сделать, когда будете вычитать вторую строку (только местами нужно будет менять второй столбец и столбец с максимальным элементом) и т.д.\n",
    "\n",
    "Для примера пройдём прямой ход для модифицированной СЛАУ из предыдущего урока:\n",
    "$$\n",
    "\\left\\{\\begin{array}{ccc}2x + y - z &=& 8 \\\\\n",
    "-3x - y + 4z &=& -11 \\\\\n",
    "-2x + y + 2z &=& -3 \\end{array}\\right.\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Задаём матрицы\n",
    "A = Float64.([2 1 -1;\n",
    "              -3 -1 4;\n",
    "              -2 1 2]);\n",
    "B = Float64.([8, -11, -3]);\n",
    "A_pr = [A B]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Для поиска максимального элемента в массиве можно использовать функцию `argmax(A)`. Она находит индекс максимального элемента в коллекции."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "q = [1, 2, -5, 10, 4, 2];\n",
    "argmax(q)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Найдём индекс максимального элемента по модулю в первой строке (учтите, нужно брать неприсоединённую матрицу). Для нахождения модуля числа используется функция `abs`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "i = argmax(abs.(A_pr[1, 1:3]))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Видно, что первый элемент самый большой, менять ничего не нужно. Поэтому делаем ту же самую операцию, что и в предыдущий раз: вычитаем первую строку из второй и из третьей."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "A_pr[2, :] -= A_pr[1, :] * A_pr[2,1] / A_pr[1,1];\n",
    "A_pr[3, :] -= A_pr[1, :] * A_pr[3,1] / A_pr[1,1];\n",
    "A_pr"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Снова ищем максимальный по модулю элемент, на этот раз во второй строке."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "i = argmax(abs.(A_pr[2, 2:3]))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Номер максимального элемента второй, но мы начинали со второго столбца, поэтому максимальный по модулю элемент находится в третьем столбце. Меняем местами третий и второй столбцы (номер столбца с максимальным элементом равен i+1). Обратите внимание на приём, который используется для того, чтобы поменять местами столбцы: их объединяют в кортеж."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "A_pr[:, 3], A_pr[:, 2] = A_pr[:, 2], A_pr[:, 3];\n",
    "A_pr"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Осталось вычесть вторую строку из третьей."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "A_pr[3, :] -= A_pr[2, :] * A_pr[3,2] / A_pr[2,2];\n",
    "A_pr"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Упражнение 1\n",
    "Напишите функцию `gauss_main_row(A::AbstractMatrix, B::AbstractVector)::AbstractVector`, которая принимает матрицу A и вектор B и возвращает вектор неизвестных с помощью метода Гаусса с выбором главного элемента по строке. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function gauss_main_row(A::AbstractMatrix, B::AbstractVector)::Vector{Float64}\n",
    "    n = size(A,1)\n",
    "    X = zeros(Float64, n) # задаём вектор X из нулей\n",
    "    ord_X = collect(1:n)\n",
    "    \n",
    "    A_pr = [A B] # делаем присоединённую матрицу\n",
    "    \n",
    "    for i in 1:n-1          # главный цикл - осуществляет операцию для каждой строки\n",
    "        k = argmax(abs.(A_pr[i, i:end-1]))\n",
    "        if i!=k\n",
    "            A_pr[:, k+i-1], A_pr[:, i] = A_pr[:, i], A_pr[:, k+i-1]\n",
    "            ord_X[i], ord_X[k+i-1] = ord_X[k+i-1], ord_X[i]\n",
    "        end\n",
    "        for j in i+1:n      # вложенный цикл для вычитания\n",
    "            A_pr[j, :] .-= A_pr[i, :] / A_pr[i, i] * A_pr[j, i] # из j-й строки вычесть i-ю, умноженную на соответствующие коэффициенты. Чтобы сделать это со всеми столбацами, используется срез.\n",
    "        end\n",
    "    end\n",
    "    A_new = A_pr[1:end, 1:end-1] # Разделяем полученную матрицу\n",
    "    B_new = A_pr[1:end, end]\n",
    "    \n",
    "    X[n] = B_new[n]/A_new[n, n] # находим последний X\n",
    "    for i in n-1:-1:1 \n",
    "        X[i] = (B_new[i] - sum(A_new[i, i+1:n] .* X[i+1:n]))/A_new[i,i] # последовательно, начиная с конца, находим X[i]. Используем значение X, полученное на предыдущем шаге\n",
    "    end\n",
    "    \n",
    "    X_new = zeros(Float64, n)\n",
    "    X_new[ord_X] .= X\n",
    "    return X_new\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "A = Float64.([2 1 -1;\n",
    "              -3 -1 2;\n",
    "              -2 1 2]);\n",
    "\n",
    "B = Float64.([8, -11, -3])\n",
    "X = gauss_main_row(A, B)\n",
    "@assert X ≈ A \\ B"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Упражнение 2\n",
    "По аналогии с предыдущим, создайте функцию `gauss_main_col(A::AbstractMatrix, B::AbstractVector)::AbstractVector`, которая принимает матрицу A и вектор B и возвращает вектор неизвестных с помощью метода Гаусса с выбором главного элемента по столбцу. Она должна отличаться парой строк в цикле прямого хода (искать главный элемент нужно по столбцам и менять местами не столбцы, а строки). Также, когда меняются местами строки в матрицах A и B, вектор X не меняется."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function gauss_main_col(A::AbstractMatrix, B::AbstractVector)::Vector{Float64}\n",
    "    n = size(A,1)\n",
    "    X = zeros(Float64, n) # задаём вектор X из нулей\n",
    "    \n",
    "    A_pr = [A B] # делаем присоединённую матрицу\n",
    "    \n",
    "    for i in 1:n-1          # главный цикл - осуществляет операцию для каждой строки\n",
    "        k = argmax(abs.(A_pr[i:end, i]))\n",
    "\n",
    "        if i!=k\n",
    "            A_pr[k+i-1, :], A_pr[i, :] = A_pr[i, :], A_pr[k+i-1, :]\n",
    "        end\n",
    "        for j in i+1:n      # вложенный цикл для вычитания\n",
    "            A_pr[j, :] .-= A_pr[i, :] / A_pr[i, i] * A_pr[j, i] # из j-й строки вычесть i-ю, умноженную на соответствующие коэффициенты. Чтобы сделать это со всеми столбацами, используется срез.\n",
    "        end\n",
    "    end\n",
    "    A_new = A_pr[1:end, 1:end-1] # Разделяем полученную матрицу\n",
    "    B_new = A_pr[1:end, end]\n",
    "    \n",
    "    X[n] = B_new[n]/A_new[n, n] # находим последний X\n",
    "    for i in n-1:-1:1 \n",
    "        X[i] = (B_new[i] - sum(A_new[i, i+1:n] .* X[i+1:n]))/A_new[i,i] # последовательно, начиная с конца, находим X[i]. Используем значение X, полученное на предыдущем шаге\n",
    "    end\n",
    "    \n",
    "    return X\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "A = Float64.([2 1 -1;\n",
    "              -3 -1 2;\n",
    "              -2 1 2]);\n",
    "\n",
    "B = Float64.([8, -11, -3])\n",
    "X = gauss_main_col(A, B)\n",
    "@assert X ≈ A \\ B"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.3",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.2"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
